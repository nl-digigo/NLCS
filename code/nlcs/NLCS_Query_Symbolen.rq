PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX schema: <https://schema.org/>

#TODO: searchterm?
# Retrieves all Symbool information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?searchterm ?sbibliotheek ?fase ?symbool ?finalCleanName ?optie   
WHERE {
  ?SymboolTopConcept a owl:Class .
  ?SymboolTopConcept skos:prefLabel "Symbool" .
  ?sbibliotheekURI rdfs:subClassOf ?SymboolTopConcept .
  ?sbibliotheekURI skos:prefLabel ?sbibliotheek .
  ?symbolTopURI rdfs:subClassOf ?sbibliotheekURI .
  ?symbolTopURI skos:prefLabel ?topSymbol .
  BIND (SUBSTR(REPLACE(?topSymbol, ?sbibliotheek, ""), 2) AS ?searchterm) .
  ?symboolURI rdfs:subClassOf+ ?symbolTopURI .
  ?symboolURI skos:prefLabel ?symbool .
  FILTER (NOT EXISTS{?subSymbool rdfs:subClassOf ?symboolURI}) .  
  OPTIONAL {
    ?symboolURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  }
  OPTIONAL {
    ?symboolURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  }
  OPTIONAL {
    ?symboolURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  } 
  OPTIONAL {
    ?symboolURI schema:url ?fileURL .
  }

  BIND (IF (
    (BOUND(?fase) && CONTAINS(?symbool, CONCAT(?fase, "-"))),
    REPLACE(?symbool, CONCAT(?fase, "-"), ""),
    ?symbool) AS ?nameWithoutFase)
  
  BIND (IF (
    CONTAINS(?nameWithoutFase, CONCAT(?sbibliotheek, "-")),
    REPLACE(?nameWithoutFase, CONCAT(?sbibliotheek, "-"), ""),
    ?nameWithoutFase) AS ?nameWithoutFaseSbibliotheek)

  BIND (IF (
    BOUND(?optie) && CONTAINS(?nameWithoutFaseSbibliotheek, CONCAT("-", ?optie)),
    REPLACE(?nameWithoutFaseSbibliotheek, CONCAT("-", ?optie), ""),
    ?nameWithoutFaseSbibliotheek) AS ?finalCleanName)

} ORDER BY ?id
