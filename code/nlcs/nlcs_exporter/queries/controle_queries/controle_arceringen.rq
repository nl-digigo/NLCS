PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX schema: <https://schema.org/>

# Checks:
#   - existence & availability of ?id ?schaal ?vrkl_lang and ?fileURL
#   - if omschrijving (name) starts with B-, V-, N-, O- or T, this value should be available as separate value as fase
#   - if omschrijving (name) ends with -SO, -SOD, -SODMM , this value should be available as separate value as optie

SELECT DISTINCT ?arceringURI ?id ?abibliotheekURI ?abibliotheek ?fase ?arcering ?optie ?schaal ?vrkl_kort ?vrkl_lang ?fileURL
WHERE {
  ?ArceringTopConcept a owl:Class .
  ?ArceringTopConcept skos:prefLabel "Arcering" .
  ?abibliotheekURI rdfs:subClassOf ?ArceringTopConcept .
  ?abibliotheekURI skos:definition ?omschrijving .
  ?abibliotheekURI skos:prefLabel ?abibliotheek .
  ?arceringURI rdfs:subClassOf+ ?abibliotheekURI .
  ?arceringURI skos:prefLabel ?arcering .
  FILTER( NOT EXISTS { ?subArcering rdfs:subClassOf ?arceringURI . }) .
  OPTIONAL {
    ?arceringURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  } 
  OPTIONAL {
    ?arceringURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  } 
  OPTIONAL {
    ?arceringURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  } 
  OPTIONAL {
    ?arceringURI ?vrkl_kortProperty ?vrkl_kort .
    ?vrkl_kortProperty skos:prefLabel "VRKL_KORT" .
  } 
  OPTIONAL {
    ?arceringURI ?vrkl_langProperty ?vrkl_lang .
    ?vrkl_langProperty skos:prefLabel "VRKL_LANG" .
  }
  OPTIONAL {
    ?arceringURI ?schaalProperty ?schaal .
    ?schaalProperty skos:prefLabel "Schaal" .
  } 
  OPTIONAL {
    ?arceringURI schema:url ?fileURL .
  } 

  FILTER((!BOUND(?id) || (STRLEN(STR(?id)) = 0)) 
        || 
        (!BOUND(?schaal) || (STRLEN(STR(?schaal)) = 0)) 
        || 
        (!BOUND(?vrkl_lang) || (STRLEN(STR(?vrkl_lang)) = 0)) 
        || 
        (!BOUND(?fileURL) || (STRLEN(STR(?fileURL)) = 0))
        ||
        (STRSTARTS(?arcering, "B-") && (str(?fase) != "B"))
        ||
        (STRSTARTS(?arcering, "N-") && (str(?fase) != "N"))
        ||
        (STRSTARTS(?arcering, "V-") && (str(?fase) != "V"))
        ||
        (STRSTARTS(?arcering, "O-") && (str(?fase) != "O"))
        ||
        (STRSTARTS(?arcering, "T-") && (str(?fase) != "T"))
        ||
        (STRENDS(?arcering, "-SODMM") && (str(?optie) != "SODMM"))
        ||
        (STRENDS(?arcering, "-SO") && (str(?optie) != "SO"))
        ||
        (STRENDS(?arcering, "-SOD") && (str(?optie) != "SOD"))
  )
} ORDER BY ?id
