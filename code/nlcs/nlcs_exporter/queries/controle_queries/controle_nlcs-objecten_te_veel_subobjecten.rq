PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves the NLCS-objects with more than 5 subobjects. (If there are any, the NLCS-object query needs to be updated.)

SELECT DISTINCT ?objectURI ?hoofdgroep ?omschrijving ?numSubobjects
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  ?hoofdgroepURI rdfs:subClassOf ?NLCSObjectTopConcept .
  ?hoofdgroepURI skos:prefLabel ?hoofdgroep .
  ?objectURI rdfs:subClassOf+ ?hoofdgroepURI .
  ?objectURI skos:prefLabel ?omschrijving .
  
  BIND (IF(CONTAINS(?omschrijving, "-"), STRBEFORE(?omschrijving, "-"), ?omschrijving) AS ?objectClean) .
  BIND (STRLEN(REPLACE(?objectClean, "[^_]", "")) AS ?numSubobjects) .
  FILTER (?numSubobjects > 5) . 
}
