PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Retrieves NLCS-objects with relations to both parent & child Symbolen and/or Arceringen. The listed saobject is the parent of which also children are linked

SELECT DISTINCT ?hoofdgroep ?omschrijving ?type ?saobject ?objectURI
WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  ?hoofdgroepURI rdfs:subClassOf ?NLCSObjectTopConcept .
  ?hoofdgroepURI skos:prefLabel ?hoofdgroep .
  ?objectURI rdfs:subClassOf+ ?hoofdgroepURI .
  ?objectURI skos:prefLabel ?omschrijving .

  ?objectURI rdfs:subClassOf ?bnodeMain .
  ?bnodeMain owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
  ?bnodeMain owl:onClass ?saobjectMainURI .
  ?saobjectMainURI rdfs:subClassOf* ?topConcept .
  ?topConcept skos:prefLabel ?type .
  FILTER( ?type IN ("Symbool", "Arcering")) .
  ?saobjectMainURI skos:prefLabel ?saobject .
  
  ?objectURI rdfs:subClassOf ?bnodeExtra .
  ?bnodeExtra owl:onProperty <https://w3id.org/nen2660/def#isDescribedBy> .
  ?bnodeExtra owl:onClass ?saobjectExtraURI .
  ?saobjectExtraURI rdfs:subClassOf+ ?saobjectMainURI .
}
