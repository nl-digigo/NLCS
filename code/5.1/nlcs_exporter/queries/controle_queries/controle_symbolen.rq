PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX schema: <https://schema.org/>

# Checks:
#   - existence & availability of ?id
#   - if omschrijving (name) starts with B-, V-, N-, O- or T, this value should be available as separate value as fase
#   - if omschrijving (name) ends with -S, -SO, -D, - DMM,-MM, -SOD, -SODMM, -SOMM, this value should be available as separate value as optie

#SELECT DISTINCT ?symboolURI ?id ?sbibliotheek ?sbibliotheekURI ?fase ?symbool ?optie ?fileURL
SELECT DISTINCT ?sbibliotheek ?symbool ?fase ?id ?optie ?fileURL
WHERE {
  ?SymboolTopConcept a owl:Class .
  ?SymboolTopConcept skos:prefLabel "Symbool" .
  ?sbibliotheekURI rdfs:subClassOf ?SymboolTopConcept .
  ?sbibliotheekURI skos:prefLabel ?sbibliotheek .
  ?symboolURI rdfs:subClassOf+ ?sbibliotheekURI .
  ?symboolURI skos:prefLabel ?symbool .
  FILTER (NOT EXISTS{?subSymbool rdfs:subClassOf ?symboolURI}) .  
  OPTIONAL {
    ?symboolURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  }
  OPTIONAL {
    ?symboolURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  }
  OPTIONAL {
    ?symboolURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  } 
  
 OPTIONAL {
    ?symboolURI schema:url ?fileURL .
  } 
  
  FILTER (
	  # missing ID
    (
      !BOUND(?id)
    ) ||
    # missing/incorrect fase
 	  ( 
      (STRSTARTS(?symbool, "B-") && (!BOUND(?fase) || ?fase != "B")) ||
      (STRSTARTS(?symbool, "V-") && (!BOUND(?fase) || ?fase != "V")) ||
      (STRSTARTS(?symbool, "N-") && (!BOUND(?fase) || ?fase != "N")) ||
      (STRSTARTS(?symbool, "O-") && (!BOUND(?fase) || ?fase != "O")) ||
      (STRSTARTS(?symbool, "T-") && (!BOUND(?fase) || ?fase != "T"))
    ) ||
    # missing/incorrect optie
    ( 
      (STRENDS(?symbool, "-S") && (!BOUND(?optie) || ?optie != "S")) ||
      (STRENDS(?symbool, "-SO") && (!BOUND(?optie) || ?optie != "SO")) ||
      (STRENDS(?symbool, "-D") && (!BOUND(?optie) || ?optie != "D")) ||
      (STRENDS(?symbool, "-DMM") && (!BOUND(?optie) || ?optie != "DMM")) ||
      (STRENDS(?symbool, "-MM") && (!BOUND(?optie) || ?optie != "MM")) ||
      (STRENDS(?symbool, "-SOD") && (!BOUND(?optie) || ?optie != "SOD")) ||
      (STRENDS(?symbool, "-SODMM") && (!BOUND(?optie) || ?optie != "SODMM")) ||
      (STRENDS(?symbool, "-SOMM") && (!BOUND(?optie) || ?optie != "SOMM"))  
    )
  )
} ORDER BY ?id
