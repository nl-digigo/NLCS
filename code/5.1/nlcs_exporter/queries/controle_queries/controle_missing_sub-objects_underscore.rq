PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

SELECT ?hoofdgroep ?object ?parent ?num_underscore_object ?num_underscore_parent WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  
  ?hoofdgroepURI rdfs:subClassOf ?NLCSObjectTopConcept .
  ?hoofdgroepURI skos:prefLabel ?hoofdgroep .
  #FILTER(?hoofdgroep = "AL")

  ?topObjectURI rdfs:subClassOf ?hoofdgroepURI .
  ?topObjectURI skos:prefLabel ?topObject .
  
  ?objectURI rdfs:subClassOf+ ?topObjectURI .
  ?objectURI skos:prefLabel ?object .

  ?objectURI rdfs:subClassOf ?parentURI .
  ?parentURI skos:prefLabel ?parent .
  FILTER(contains(?object, "_")) .

  BIND(strlen(?object) - strlen(replace(?object, "_", "")) AS ?num_underscore_object)
  BIND(strlen(?parent) - strlen(replace(?parent, "_", "")) AS ?num_underscore_parent)

  BIND (IF ((?num_underscore_object != ?num_underscore_parent + 1), "yes", "no") AS ?missing_sub_object)
  FILTER(!contains(?object, "-") && !contains(?object, " ") && (?missing_sub_object = "yes")) .
}
ORDER BY ?hoofdgroep ?object
