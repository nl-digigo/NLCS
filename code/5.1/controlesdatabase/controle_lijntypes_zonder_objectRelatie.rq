PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ls: <http://www.laces.tech/publication/ns/semmtech/live/laces/schema/shapes/>
PREFIX lbm: <http://www.laces.tech/publication/ns/semmtech/live/laces/schema/blueprint-manager/>
PREFIX otl: <http://www.laces.tech/publication/ns/semmtech/live/laces/schema/otl-manager/>

SELECT ?hoofdgroepNaam ?lijntypeNaam

WHERE {
  ?topLijntype a otl:Document .
  ?topLijntype otl:attr-Conceptual-name "Lijntype" .
  ?hoofdgroepTemp otl:rel-Conceptual-isSpecializationOf ?topLijntype .
  ?hoofdgroepTemp otl:attr-Conceptual-name ?hoofdgroepNaamTemp .
  ?lijntype otl:rel-Conceptual-isSpecializationOf* ?hoofdgroepTemp .
  ?lijntype otl:attr-Conceptual-name ?lijntypeNaam .
  
  BIND ( IF(?lijntype = ?hoofdgroepTemp, IF(?lijntypeNaam IN ("V-CONTINUOUS-SO", "CONTINUOUS", "NLCS_HIDDEN-SO"), ?lijntype, 1/0), ?lijntype) AS ?NotHoofdgroep) .
  BIND ( IF(?lijntype = ?hoofdgroepTemp, IF(?lijntypeNaam IN ("V-CONTINUOUS-SO", "CONTINUOUS", "NLCS_HIDDEN-SO"), 1/0, ?hoofdgroepNaamTemp), ?hoofdgroepNaamTemp) AS ?hoofdgroepNaam) .
  FILTER (?lijntype = ?NotHoofdgroep) .
  
  FILTER NOT EXISTS {
    ?topLaagnaam a otl:PhysicalObject .
    ?topLaagnaam otl:attr-Conceptual-name "NLCS-object" .
    ?hgLaagnaam otl:rel-Conceptual-isSpecializationOf ?topLaagnaam .
    ?laagnaam otl:rel-Conceptual-isSpecializationOf+ ?hgLaagnaam .
    
    ?laagnaam otl:rel-Conceptual-isDescribedIn ?infoField .
    ?infoField a otl:InformationField .
    ?infoField otl:rel-InformationField-isClassifiedAs ?infoType .
    ?infoType otl:attr-Conceptual-name ?infoTypeNaam .
    FILTER (CONTAINS(?infoTypeNaam, "linetype")) .
    ?infoField otl:attr-InformationField-value ?infoFieldValue .
    FILTER (?infoFieldValue = ?lijntypeNaam) .
    }
  
}