PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX schema: <https://schema.org/>

# Retrieves all Arcering information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?arceringURI ?id ?abibliotheekURI ?abibliotheek ?fase ?arcering ?finalCleanName ?optie ?schaal ?vrkl_kort ?vrkl_lang ?fileURL
WHERE {
  ?ArceringTopConcept a owl:Class .
  ?ArceringTopConcept skos:prefLabel "Arcering" .
  ?abibliotheekURI rdfs:subClassOf ?ArceringTopConcept .
  ?abibliotheekURI skos:definition ?omschrijving .
  ?abibliotheekURI skos:prefLabel ?abibliotheek .
  ?arceringURI rdfs:subClassOf+ ?abibliotheekURI .
  ?arceringURI skos:prefLabel ?arcering .
  FILTER( NOT EXISTS { ?subArcering rdfs:subClassOf ?arceringURI . }) .
  OPTIONAL {
    ?arceringURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  } 
  OPTIONAL {
    ?arceringURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  } 
  OPTIONAL {
    ?arceringURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  } 
  OPTIONAL {
    ?arceringURI ?vrkl_kortProperty ?vrkl_kort .
    ?vrkl_kortProperty skos:prefLabel "VRKL_KORT" .
  } 
  OPTIONAL {
    ?arceringURI ?vrkl_langProperty ?vrkl_lang .
    ?vrkl_langProperty skos:prefLabel "VRKL_LANG" .
  }
  OPTIONAL {
    ?arceringURI ?schaalProperty ?schaal .
    ?schaalProperty skos:prefLabel "Schaal" .
  } 
  OPTIONAL {
    ?arceringURI schema:url ?fileURL .
  } 
  
  BIND (IF (
            (BOUND(?fase) && CONTAINS(?arcering, ?fase)), 
            SUBSTR(REPLACE(?arcering, ?fase, ""), 2),
            ?arcering) AS ?nameWithoutFase)

  BIND( IF(BOUND(?nameWithoutFase), STRLEN(?nameWithoutFase)-1, STRLEN(?arcering)-1) AS ?len)
  
  BIND(
    IF(
      BOUND(?nameWithoutFase),
      IF(
        BOUND(?abibliotheek) && CONTAINS(?nameWithoutFase, ?abibliotheek),
        SUBSTR(REPLACE(?nameWithoutFase, ?abibliotheek, ""), 2),
        ?nameWithoutFase
      ),
      IF(
        BOUND(?abibliotheek) && CONTAINS(?arcering, ?abibliotheek),
        SUBSTR(REPLACE(?arcering, ?abibliotheek, ""), 2),
        ?arcering 
      )
    ) AS ?nameWithoutFaseAbibliotheek
  )

  BIND( IF(BOUND(?nameWithoutFaseAbibliotheek), 
          STRLEN(?nameWithoutFaseAbibliotheek)-1, 
          IF(BOUND(?nameWithoutFase), STRLEN(?nameWithoutFase)-1, STRLEN(?arcering)-1)
          ) AS ?len2)
  
  BIND(
    IF(
      BOUND(?nameWithoutFaseAbibliotheek),
      IF(
        BOUND(?optie) && CONTAINS(?nameWithoutFaseAbibliotheek, ?optie),
        SUBSTR(REPLACE(?nameWithoutFaseAbibliotheek, ?optie, ""), 1, (?len2 - strlen(?optie))),
        ?nameWithoutFaseAbibliotheek
      ),
      IF(
        BOUND(?nameWithoutFase),
        IF( BOUND(?optie) && CONTAINS(?nameWithoutFase, ?optie),
            SUBSTR(REPLACE(?nameWithoutFase, ?optie, ""), 1, (?len2 - strlen(?optie))),
            ?nameWithoutFase
        ),
        IF(
          BOUND(?optie) && CONTAINS(?arcering, ?optie),
          SUBSTR(REPLACE(?arcering, ?optie, ""), 1, (?len2 - strlen(?optie))),
          ?arcering 
        )
      )
    ) AS ?finalCleanName
  )

} ORDER BY ?id
