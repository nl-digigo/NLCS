PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX schema: <https://schema.org/>

# Retrieves all Arcering information and stores it in the familiar Excel/CSV format from the SQL database

SELECT DISTINCT ?searchterm ?arceringURI ?id ?abibliotheekURI ?abibliotheek ?fase ?arcering ?finalCleanName ?optie ?schaal ?vrkl_kort ?vrkl_lang ?fileURL
WHERE {
  ?ArceringTopConcept a owl:Class .
  ?ArceringTopConcept skos:prefLabel "Arcering" .
  ?abibliotheekURI rdfs:subClassOf ?ArceringTopConcept .
  ?abibliotheekURI skos:definition ?omschrijving .
  ?abibliotheekURI skos:prefLabel ?abibliotheek .
  ?arceringTopURI rdfs:subClassOf ?abibliotheekURI .
  ?arceringTopURI skos:prefLabel ?topArcering .
  BIND (SUBSTR(REPLACE(?topArcering, ?abibliotheek, ""), 2) AS ?searchterm) .
  ?arceringURI rdfs:subClassOf+ ?arceringTopURI .
  ?arceringURI skos:prefLabel ?arcering .
  FILTER( NOT EXISTS { ?subArcering rdfs:subClassOf ?arceringURI . }) .
  OPTIONAL {
    ?arceringURI ?faseProperty ?fase .
    ?faseProperty skos:prefLabel "Fase" .
  } 
  OPTIONAL {
    ?arceringURI ?optieProperty ?optie .
    ?optieProperty skos:prefLabel "Optie" .
  } 
  # OPTIONAL {  # no longer optional
    ?arceringURI ?idProperty ?id .
    ?idProperty skos:prefLabel "ID" .
  # } 
  OPTIONAL {
    ?arceringURI ?vrkl_kortProperty ?vrkl_kort .
    ?vrkl_kortProperty skos:prefLabel "VRKL_KORT" .
  } 
  OPTIONAL {
    ?arceringURI ?vrkl_langProperty ?vrkl_lang .
    ?vrkl_langProperty skos:prefLabel "VRKL_LANG" .
  }
  OPTIONAL {
    ?arceringURI ?schaalProperty ?schaal .
    ?schaalProperty skos:prefLabel "Schaal" .
  } 
  OPTIONAL {
    ?arceringURI schema:url ?fileURL .
  } 
  
  BIND (IF (
    (BOUND(?fase) && CONTAINS(?arcering, CONCAT(?fase, "-"))),
    REPLACE(?arcering, CONCAT(?fase, "-"), ""),
    ?arcering) AS ?nameWithoutFase)
  
  BIND (IF (
    CONTAINS(?nameWithoutFase, CONCAT(?abibliotheek, "-")),
    REPLACE(?nameWithoutFase, CONCAT(?abibliotheek, "-"), ""),
    ?nameWithoutFase) AS ?nameWithoutFaseAbibliotheek)

  BIND (IF (
    BOUND(?optie) && CONTAINS(?nameWithoutFaseAbibliotheek, CONCAT("-", ?optie)),
    REPLACE(?nameWithoutFaseAbibliotheek, CONCAT("-", ?optie), ""),
    ?nameWithoutFaseAbibliotheek) AS ?finalCleanName)

} ORDER BY ?id
